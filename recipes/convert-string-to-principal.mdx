---
id: convert-string-to-principal
title: Convert string to principal
description: How to convert a string address to a principal type.
date: 2025.01.16
categories:
  - clarity
tags:
  - principals
  - strings
dependencies: []
files:
  - name: convert-string-to-principal.clar
    path: contracts/convert-string-to-principal.clar
    type: clarity
external_url: https://play.hiro.so/?epoch=3.1&snippet=KGRlZmluZS1jb25zdGFudCBBTExfSEVYIDB4MDAwMTAyMDMwNDA1MDYwNzA4MDkwQTBCMEMwRDBFMEYxMDExMTIxMzE0MTUxNjE3MTgxOTFBMUIxQzFEMUUxRjIwMjEyMjIzMjQyNTI2MjcyODI5MkEyQjJDMkQyRTJGMzAzMTMyMzMzNDM1MzYzNzM4MzkzQTNCM0MzRDNFM0Y0MDQxNDI0MzQ0NDU0NjQ3NDg0OTRBNEI0QzRENEU0RjUwNTE1MjUzNTQ1NTU2NTc1ODU5NUE1QjVDNUQ1RTVGNjA2MTYyNjM2NDY1NjY2NzY4Njk2QTZCNkM2RDZFNkY3MDcxNzI3Mzc0NzU3Njc3Nzg3OTdBN0I3QzdEN0U3RjgwODE4MjgzODQ4NTg2ODc4ODg5OEE4QjhDOEQ4RThGOTA5MTkyOTM5NDk1OTY5Nzk4OTk5QTlCOUM5RDlFOUZBMEExQTJBM0E0QTVBNkE3QThBOUFBQUJBQ0FEQUVBRkIwQjFCMkIzQjRCNUI2QjdCOEI5QkFCQkJDQkRCRUJGQzBDMUMyQzNDNEM1QzZDN0M4QzlDQUNCQ0NDRENFQ0ZEMEQxRDJEM0Q0RDVENkQ3RDhEOURBREJEQ0REREVERkUwRTFFMkUzRTRFNUU2RTdFOEU5RUFFQkVDRURFRUVGRjBGMUYyRjNGNEY1RjZGN0Y4RjlGQUZCRkNGREZFRkYpCihkZWZpbmUtY29uc3RhbnQgQzMyX0NIQVJTICIwMTIzNDU2Nzg5QUJDREVGR0hKS01OUFFSU1RWV1hZWiIpCgooZGVmaW5lLWNvbnN0YW50IEVSUl8xMDAgKHRocm93IHUxMDApKSA7OyBpbnZhbGlkIGlucHV0IGRhdGEgKGludmFsaWQgY2hhcmFjdGVyKQooZGVmaW5lLWNvbnN0YW50IEVSUl8xMDEgKHRocm93IHUxMDEpKSA7OyBkYXRhIHRvbyBzaG9ydCAoaW52YWxpZCBhZGRyZXNzIGxlbmd0aCkKKGRlZmluZS1jb25zdGFudCBFUlJfMTAyICh0aHJvdyB1MTAyKSkgOzsgZGF0YSB0b28gbG9uZyAoaW52YWxpZCBhZGRyZXNzIGxlbmd0aCkKKGRlZmluZS1jb25zdGFudCBFUlJfMTAzICh0aHJvdyB1MTAzKSkgOzsgaW52YWxpZCBmaW5hbCBsZW5naHQgKHRvbyBzaG9ydCBvciB0b28gbG9uZykKKGRlZmluZS1jb25zdGFudCBFUlJfMTA0ICh0aHJvdyB1MTA0KSkgOzsgaW52YWxpZCBjb250cmFjdCBuYW1lIGxlbmdodAooZGVmaW5lLWNvbnN0YW50IEVSUl8xMDUgKHRocm93IHUxMDUpKSA7OyBpbnZhbGlkIGNoZWNrc3VtCgoKKGRlZmluZS1yZWFkLW9ubHkgKHN0cmluZy10by1wcmluY2lwYWw_IChpbnB1dCAoc3RyaW5nLWFzY2lpIDgyKSkpCiAgKGxldCAKICAgICgKICAgICAgKGRvdCAoZGVmYXVsdC10byAobGVuIGlucHV0KSAoaW5kZXgtb2Y_IGlucHV0ICIuIikpKQogICAgICAoYWRkciAodW53cmFwISAoc2xpY2U_IGlucHV0IHUxIGRvdCkgRVJSXzEwMSkpCiAgICAgIChhZGRyZXNzTGVuIChsZW4gYWRkcikpCiAgICAgIDs7IGNoZWNrIGlmIGFkZHJlc3MgaXMgbm90IHRvbyBsb25nCiAgICAgIChpc1ZhbGlkTGVuIChhc3NlcnRzISAoPD0gYWRkcmVzc0xlbiB1NDApIEVSUl8xMDIpKQogICAgICAoYWRkcmVzc2MzMiAobWFwIHVud3JhcC1wYW5pYy11aW50IChmaWx0ZXIgaXMtc29tZS11aW50IChtYXAgYzMyLWluZGV4IGFkZHIpKSkpCiAgICAgIDs7IGNoZWNrIGlmIGFsbCBjaGFycyBiZWxvbmdzIHRvIENSMzIKICAgICAgKGlzVmFsaWRDaGFycyAoYXNzZXJ0cyEgKGlzLWVxIGFkZHJlc3NMZW4gKGxlbiBhZGRyZXNzYzMyKSkgRVJSXzEwMCkpIAogICAgICAodmVyc2lvbiAodW53cmFwLXBhbmljIChlbGVtZW50LWF0PyBhZGRyZXNzYzMyIHUwKSkpIDs7IGNhbid0IGZhaWwKICAgICAgKGFkZHJlc3NEYXRhICh1bndyYXAtcGFuaWMgKGFzLW1heC1sZW4_ICh1bndyYXAhIChzbGljZT8gYWRkcmVzc2MzMiB1MSBhZGRyZXNzTGVuKSBFUlJfMTAxKSB1MzkpKSkKICAgICAgKGRlY29kZWQgKGZvbGQgZGVjb2RlIChyZXZlcnNlLWxpc3QtdWludCBhZGRyZXNzRGF0YSkgKGxpc3QgdTAgdTApKSkKICAgICAgKGZpbmFsRGVjb2RlZCAoY29uY2F0IChpZiAoPCB1MCAodW53cmFwLXBhbmljIChlbGVtZW50LWF0PyBkZWNvZGVkIHUxKSkpIChsaXN0ICh1bndyYXAtcGFuaWMgKGVsZW1lbnQtYXQ_IGRlY29kZWQgdTApKSkgKGxpc3QpKSAodW53cmFwISAoc2xpY2U_IGRlY29kZWQgdTIgKGxlbiBkZWNvZGVkKSkgRVJSXzEwMSkpKQogICAgICAoZmluYWxEZWNvZGVkTGVuIChsZW4gZmluYWxEZWNvZGVkKSkKICAgICAgKGZpbmFsIChjb25jYXQKICAgICAgICA7OyBsZWFkaW5nIHplcm9zIGZyb20gaW5wdXQKICAgICAgICAoZGVmYXVsdC10byAobGlzdCkgKHNsaWNlPyBhZGRyZXNzRGF0YSB1MCAoZGVmYXVsdC10byB1MCAoaW5kZXgtb2Y_IChtYXAgaXMtemVybyBhZGRyZXNzRGF0YSkgZmFsc2UpKSkpIAogICAgICAgIDs7IGZpbmFsRGVjb2RlZCB3aXRob3V0IGxlYWRpbmcgemVyb3MKICAgICAgICAoZGVmYXVsdC10byAobGlzdCkgKHNsaWNlPyBmaW5hbERlY29kZWQgKGRlZmF1bHQtdG8gZmluYWxEZWNvZGVkTGVuIChpbmRleC1vZj8gKG1hcCBpcy16ZXJvIGZpbmFsRGVjb2RlZCkgZmFsc2UpKSBmaW5hbERlY29kZWRMZW4pKQogICAgICApKQogICAgICAoZmluYWxMZW4gKGxlbiBmaW5hbCkpCiAgICAgIDs7IGNoZWNrIGlmIGZpbmFsIGxlbmd0aCBpcyBub3QgdG9vIHNob3J0IG9yIHRvbyBsb25nCiAgICAgIChpc0ZpbmFsTGVuVmFsaWQgKGFzc2VydHMhIChhbmQgKDw9IGZpbmFsTGVuIHUyNCkgKDw9IHU1IGZpbmFsTGVuKSkgRVJSXzEwMykpIAogICAgICAoZGF0YSAoZm9sZCB0by1oZXggZmluYWwgMHgpKQogICAgICAoZGF0YUJ5dGVzICh1bndyYXAtcGFuaWMgKGFzLW1heC1sZW4_ICh1bndyYXAtcGFuaWMgKHNsaWNlPyBkYXRhIHUwICgtIGZpbmFsTGVuIHU0KSkpIHUyMCkpKQogICAgICAoY2hlY2tzdW0gKHVud3JhcC1wYW5pYyAoc2xpY2U_IGRhdGEgKC0gZmluYWxMZW4gdTQpIGZpbmFsTGVuKSkpCiAgICAgICh2ZXJCeXRlICh1bndyYXAtcGFuaWMgKGVsZW1lbnQtYXQ_IEFMTF9IRVggdmVyc2lvbikpKQogICAgKQogICAgKGFzc2VydHMhIChpcy1lcSBjaGVja3N1bSAodW53cmFwLXBhbmljIChzbGljZT8gKHNoYTI1NiAoc2hhMjU2IChjb25jYXQgdmVyQnl0ZSBkYXRhQnl0ZXMpKSkgdTAgdTQpKSkgRVJSXzEwNSkKICAgIChtYXRjaCAoc2xpY2U_IGlucHV0ICgrIHUxIGRvdCkgKGxlbiBpbnB1dCkpIGNvbnRyYWN0CiAgICAgIChwcmluY2lwYWwtY29uc3RydWN0PyB2ZXJCeXRlIGRhdGFCeXRlcyAodW53cmFwISAoYXMtbWF4LWxlbj8gY29udHJhY3QgdTQwKSBFUlJfMTA0KSkKICAgICAgKHByaW5jaXBhbC1jb25zdHJ1Y3Q_IHZlckJ5dGUgZGF0YUJ5dGVzKQogICAgKQogICkKKQoKOzsgKCgzOSAqIDUpIC84KSA9IDI0Cjs7IDI0ICsgMiBleHRyYSBmb3IgY2FycnkgYW5kIGNhcnJ5Qml0cwooZGVmaW5lLXJlYWQtb25seSAoZGVjb2RlIChpIHVpbnQpIChvdXQgKGxpc3QgMjYgdWludCkpKQogIChsZXQgKAogICAgKGNiICh1bndyYXAtcGFuaWMgKGVsZW1lbnQtYXQ_IG91dCB1MSkpKQogICAgKGNhcnJ5ICgrICh1bndyYXAtcGFuaWMgKGVsZW1lbnQtYXQ_IG91dCB1MCkpIChiaXQtc2hpZnQtbGVmdCBpIGNiKSkpCiAgICAoY2FycnlCaXRzICgrIHU1IGNiKSkKICApIAogICAgKHVud3JhcC1wYW5pYyAoYXMtbWF4LWxlbj8gKGNvbmNhdAogICAgICAoaWYgKD49IGNhcnJ5Qml0cyB1OCkKICAgICAgICAobGlzdCAoYml0LXNoaWZ0LXJpZ2h0IGNhcnJ5IHU4KSAoLSBjYXJyeUJpdHMgdTgpIChiaXQtYW5kIGNhcnJ5ICgtIChiaXQtc2hpZnQtbGVmdCB1MSB1OCkgdTEpKSkKICAgICAgICAobGlzdCBjYXJyeSBjYXJyeUJpdHMpCiAgICAgICkKICAgICAgKGRlZmF1bHQtdG8gKGxpc3QpIChzbGljZT8gb3V0IHUyIChsZW4gb3V0KSkpKSB1MjYpKQogICkKKQoKOzsgaGVscGVycwooZGVmaW5lLXJlYWQtb25seSAodGhyb3cgKGMgdWludCkpIChlcnIgeyBlcnJvcl9jb2RlOiBjLCB2YWx1ZTogbm9uZSB9KSkKKGRlZmluZS1yZWFkLW9ubHkgKGlzLXplcm8gKGkgdWludCkpICg8PSBpIHUwKSkKKGRlZmluZS1yZWFkLW9ubHkgKGlzLXNvbWUtdWludCAoaSAob3B0aW9uYWwgdWludCkpKSAoaXMtc29tZSBpKSkKKGRlZmluZS1yZWFkLW9ubHkgKHVud3JhcC1wYW5pYy11aW50IChpIChvcHRpb25hbCB1aW50KSkpICh1bndyYXAtcGFuaWMgaSkpCihkZWZpbmUtcmVhZC1vbmx5IChjMzItaW5kZXggKGMgKHN0cmluZy1hc2NpaSAxKSkpIChpbmRleC1vZj8gQzMyX0NIQVJTIGMpKQooZGVmaW5lLXJlYWQtb25seSAodG8taGV4IChpIHVpbnQpIChvdXQgKGJ1ZmYgMjQpKSkgKHVud3JhcC1wYW5pYyAoYXMtbWF4LWxlbj8gKGNvbmNhdCBvdXQgKHVud3JhcC1wYW5pYyAoZWxlbWVudC1hdD8gQUxMX0hFWCBpKSkpIHUyNCkpKQooZGVmaW5lLXJlYWQtb25seSAocmV2ZXJzZS1saXN0LXVpbnQgKGwgKGxpc3QgMzkgdWludCkpKQogIChtYXAgdW53cmFwLXBhbmljLXVpbnQgKGZpbHRlciBpcy1zb21lLXVpbnQgKGxpc3QKICAgICAgKGVsZW1lbnQtYXQ_IGwgdTM4KSAoZWxlbWVudC1hdD8gbCB1MzcpIChlbGVtZW50LWF0IGwgdTM2KSAoZWxlbWVudC1hdD8gbCB1MzUpIChlbGVtZW50LWF0PyBsIHUzNCkgKGVsZW1lbnQtYXQ_IGwgdTMzKSAoZWxlbWVudC1hdD8gbCB1MzIpIChlbGVtZW50LWF0PyBsIHUzMSkgKGVsZW1lbnQtYXQ_IGwgdTMwKSAKICAgICAgKGVsZW1lbnQtYXQ_IGwgdTI5KSAoZWxlbWVudC1hdD8gbCB1MjgpIChlbGVtZW50LWF0PyBsIHUyNykgKGVsZW1lbnQtYXQgbCB1MjYpIChlbGVtZW50LWF0PyBsIHUyNSkgKGVsZW1lbnQtYXQ_IGwgdTI0KSAoZWxlbWVudC1hdD8gbCB1MjMpIChlbGVtZW50LWF0PyBsIHUyMikgKGVsZW1lbnQtYXQ_IGwgdTIxKSAoZWxlbWVudC1hdD8gbCB1MjApCiAgICAgIChlbGVtZW50LWF0PyBsIHUxOSkgKGVsZW1lbnQtYXQ_IGwgdTE4KSAoZWxlbWVudC1hdD8gbCB1MTcpIChlbGVtZW50LWF0IGwgdTE2KSAoZWxlbWVudC1hdD8gbCB1MTUpIChlbGVtZW50LWF0PyBsIHUxNCkgKGVsZW1lbnQtYXQ_IGwgdTEzKSAoZWxlbWVudC1hdD8gbCB1MTIpIChlbGVtZW50LWF0PyBsIHUxMSkgKGVsZW1lbnQtYXQ_IGwgdTEwKQogICAgICAoZWxlbWVudC1hdD8gbCB1MDkpIChlbGVtZW50LWF0PyBsIHUwOCkgKGVsZW1lbnQtYXQ_IGwgdTA3KSAoZWxlbWVudC1hdCBsIHUwNikgKGVsZW1lbnQtYXQ_IGwgdTA1KSAoZWxlbWVudC1hdD8gbCB1MDQpIChlbGVtZW50LWF0PyBsIHUwMykgKGVsZW1lbnQtYXQ_IGwgdTAyKSAoZWxlbWVudC1hdD8gbCB1MDEpIChlbGVtZW50LWF0PyBsIHUwMCkKICAgICkKICApKQop
---

```clarity
(define-constant ALL_HEX 0x000102030405060708090A0B0C0D0E0F101112131415161718191A1B1C1D1E1F202122232425262728292A2B2C2D2E2F303132333435363738393A3B3C3D3E3F404142434445464748494A4B4C4D4E4F505152535455565758595A5B5C5D5E5F606162636465666768696A6B6C6D6E6F707172737475767778797A7B7C7D7E7F808182838485868788898A8B8C8D8E8F909192939495969798999A9B9C9D9E9FA0A1A2A3A4A5A6A7A8A9AAABACADAEAFB0B1B2B3B4B5B6B7B8B9BABBBCBDBEBFC0C1C2C3C4C5C6C7C8C9CACBCCCDCECFD0D1D2D3D4D5D6D7D8D9DADBDCDDDEDFE0E1E2E3E4E5E6E7E8E9EAEBECEDEEEFF0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF)
(define-constant C32_CHARS "0123456789ABCDEFGHJKMNPQRSTVWXYZ")

(define-constant ERR_100 (throw u100)) ;; invalid input data (invalid character)
(define-constant ERR_101 (throw u101)) ;; data too short (invalid address length)
(define-constant ERR_102 (throw u102)) ;; data too long (invalid address length)
(define-constant ERR_103 (throw u103)) ;; invalid final lenght (too short or too long)
(define-constant ERR_104 (throw u104)) ;; invalid contract name lenght
(define-constant ERR_105 (throw u105)) ;; invalid checksum


(define-read-only (string-to-principal? (input (string-ascii 82)))
  (let 
    (
      (dot (default-to (len input) (index-of? input ".")))
      (addr (unwrap! (slice? input u1 dot) ERR_101))
      (addressLen (len addr))
      ;; check if address is not too long
      (isValidLen (asserts! (<= addressLen u40) ERR_102))
      (addressc32 (map unwrap-panic-uint (filter is-some-uint (map c32-index addr))))
      ;; check if all chars belongs to CR32
      (isValidChars (asserts! (is-eq addressLen (len addressc32)) ERR_100)) 
      (version (unwrap-panic (element-at? addressc32 u0))) ;; can't fail
      (addressData (unwrap-panic (as-max-len? (unwrap! (slice? addressc32 u1 addressLen) ERR_101) u39)))
      (decoded (fold decode (reverse-list-uint addressData) (list u0 u0)))
      (finalDecoded (concat (if (< u0 (unwrap-panic (element-at? decoded u1))) (list (unwrap-panic (element-at? decoded u0))) (list)) (unwrap! (slice? decoded u2 (len decoded)) ERR_101)))
      (finalDecodedLen (len finalDecoded))
      (final (concat
        ;; leading zeros from input
        (default-to (list) (slice? addressData u0 (default-to u0 (index-of? (map is-zero addressData) false)))) 
        ;; finalDecoded without leading zeros
        (default-to (list) (slice? finalDecoded (default-to finalDecodedLen (index-of? (map is-zero finalDecoded) false)) finalDecodedLen))
      ))
      (finalLen (len final))
      ;; check if final length is not too short or too long
      (isFinalLenValid (asserts! (and (<= finalLen u24) (<= u5 finalLen)) ERR_103)) 
      (data (fold to-hex final 0x))
      (dataBytes (unwrap-panic (as-max-len? (unwrap-panic (slice? data u0 (- finalLen u4))) u20)))
      (checksum (unwrap-panic (slice? data (- finalLen u4) finalLen)))
      (verByte (unwrap-panic (element-at? ALL_HEX version)))
    )
    (asserts! (is-eq checksum (unwrap-panic (slice? (sha256 (sha256 (concat verByte dataBytes))) u0 u4))) ERR_105)
    (match (slice? input (+ u1 dot) (len input)) contract
      (principal-construct? verByte dataBytes (unwrap! (as-max-len? contract u40) ERR_104))
      (principal-construct? verByte dataBytes)
    )
  )
)

;; ((39 * 5) /8) = 24
;; 24 + 2 extra for carry and carryBits
(define-read-only (decode (i uint) (out (list 26 uint)))
  (let (
    (cb (unwrap-panic (element-at? out u1)))
    (carry (+ (unwrap-panic (element-at? out u0)) (bit-shift-left i cb)))
    (carryBits (+ u5 cb))
  ) 
    (unwrap-panic (as-max-len? (concat
      (if (>= carryBits u8)
        (list (bit-shift-right carry u8) (- carryBits u8) (bit-and carry (- (bit-shift-left u1 u8) u1)))
        (list carry carryBits)
      )
      (default-to (list) (slice? out u2 (len out)))) u26))
  )
)

;; helpers
(define-read-only (throw (c uint)) (err { error_code: c, value: none }))
(define-read-only (is-zero (i uint)) (<= i u0))
(define-read-only (is-some-uint (i (optional uint))) (is-some i))
(define-read-only (unwrap-panic-uint (i (optional uint))) (unwrap-panic i))
(define-read-only (c32-index (c (string-ascii 1))) (index-of? C32_CHARS c))
(define-read-only (to-hex (i uint) (out (buff 24))) (unwrap-panic (as-max-len? (concat out (unwrap-panic (element-at? ALL_HEX i))) u24)))
(define-read-only (reverse-list-uint (l (list 39 uint)))
  (map unwrap-panic-uint (filter is-some-uint (list
      (element-at? l u38) (element-at? l u37) (element-at l u36) (element-at? l u35) (element-at? l u34) (element-at? l u33) (element-at? l u32) (element-at? l u31) (element-at? l u30) 
      (element-at? l u29) (element-at? l u28) (element-at? l u27) (element-at l u26) (element-at? l u25) (element-at? l u24) (element-at? l u23) (element-at? l u22) (element-at? l u21) (element-at? l u20)
      (element-at? l u19) (element-at? l u18) (element-at? l u17) (element-at l u16) (element-at? l u15) (element-at? l u14) (element-at? l u13) (element-at? l u12) (element-at? l u11) (element-at? l u10)
      (element-at? l u09) (element-at? l u08) (element-at? l u07) (element-at l u06) (element-at? l u05) (element-at? l u04) (element-at? l u03) (element-at? l u02) (element-at? l u01) (element-at? l u00)
    )
  ))
)
```